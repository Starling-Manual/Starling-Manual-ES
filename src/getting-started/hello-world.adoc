== Hola Mundo
ifndef::imagesdir[:imagesdir: ../../img]

Wow, eso fue un montón de información.
Es momento de ensuciarnos las manos!
Y que mejor forma de hacerlo que un clásico programa "Hola Mundo".
Este manual no estaría completo sin uno, no?

=== Checklist

Aquí tenemos un rápido resumen de las preparaciones que ya deberías hacer hecho:

* Elegir y descargar una IDE.
* Descargar la última versión del _AIR SDK_.
* Descargar la última versión del _Flash Player Projector_.
* Descargar la última versión del _Starling_.
* Configurar tu IDE para usar el SDK y reproductor correctos.

Configurar la IDE y crear/configurar un proyecto se hace de una forma sutilmente diferente en cada IDE.
Para ayudarte con eso, he creado un guía para cada IDE en el http://wiki.starling-framework.org/manual/[Starling Wiki].
Por favor sigue el tutorial adecuado antes de continuar.

NOTE: Lo admito, todos esos procedimientos de configuración son tediosos. Pero ten paciencia: sólo necesitas hacerlo muy rara vez.

=== Código Inicial

Crea un nuevo proyecto o módulo en tu IDE; te recomiendo comenzar con un proyecto _Flash Player_ (target platform: _Web_) con el nombre "Hello World" ("Hola Mundo" en español).
Como parte del proceso de inicialización, tu IDE va a crearte una clase inicial por vos.
Vamos a abrirla y modifiquémosla como se muestra aquí abajo.
(Típicamente, esa clase es nombrada como tu proyecto, así que cambia el nombre de la siguiente clase por el correcto.)

[source, as3]
----
package
{
    import flash.display.Sprite;
    import starling.core.Starling;

    [SWF(width="400", height="300", frameRate="60", backgroundColor="#808080")]
    public class HelloWorld extends Sprite
    {
        private var _starling:Starling;

        public function HelloWorld()
        {
            _starling = new Starling(Game, stage);
            _starling.start();
        }
    }
}
----

Este código crea una instancia Starling y la inicia inmediatamente.
Prestá atención a que le pasamos una referencia de la clase "Game" al constructor de Starling.
Starling va a instanciar esa clase una vez que este listo.
(Está hecho de esta forma para que vos no te tengas que hacer cargo de hacer las cosas en el orden correcto.)

Esa clase primero necesita ser escrita, obviamente.
Crea una nueva clase en tu proyecto llamada `Game` y agrega el siguiente código:

[source, as3]
----
package
{
    import starling.display.Quad;
    import starling.display.Sprite;
    import starling.utils.Color;

    public class Game extends Sprite
    {
        public function Game()
        {
            var quad:Quad = new Quad(200, 200, Color.RED);
            quad.x = 100;
            quad.y = 50;
            addChild(quad);
        }
    }
}
----

La clase sólo muestra un simple cuadrado rojo para ver si configuramos todo correctamente.

IMPORTANT: Observá que la clase Game extiende `starling.display.Sprite`, no `flash.display.Sprite`!
Esto es crítico, porque estamos en el mundo Starling ahora.
Está completamente separado del paquete `flash.display`.

=== Primera Ejecución

Ahora arranca el proyecto.
Para alguno de ustedes, el resultado podría ser medio deprimente, porque están viendo un mensaje de error como este:
image::startup-error.png["Startup error message", pdfwidth='7cm']

En ese caso, probablemente fue el navegador web el que se abrió en vez del proyector Flash Player.
Corroborá la configuración de ejecución/depuración [run/debug] y asegurate de que _Flash Player Projector_ (versión depurador) está siendo usada, y no el navegador.
Eso debería arreglar el problema.

==== Arreglando el error del navegador

Seguramente algún día, vas a querer embeber tu archivo SWF dentro de una página HTML.
En ese caso, puedes arreglar el error cambiando el parámetro Flash `wmode` a `direct` en el archivo HTML que está embebiendo el archivo SWF.
Típicamente, esto significa que tenés que aplicar el siguiente cambio:

[source, js]
----
// encuentra la siguiente línea ...
var params = {};

// ... y agrega está inmediamente debajo:
params.wmode = "direct";
----

==== Arreglando el error en AIR

También vas a ver este error si creaste una aplicación AIR en vez de un archivo SWF.
En ese caso, vas a tener que editar el "AIR application descriptor", que probablemente se llame `HelloWorld-app.xml` o algo parecido.
Busca el nodo XML `renderMode` (podría estar comentado) y cambia el valor por `direct`.

[source, xml]
----
Busca esto:
<!-- <renderMode></renderMode> -->

Reemplázalo por esto:
<renderMode>direct</renderMode>
----

NOTE: Lo que hemos hecho aquí es darle permiso al runtime para acceder al GPU.
Sin esos cambios, _Stage3D_ no es accesible.

=== Ejecución Arreglada

Felicitaciones! Haz compilado y ejecutado exitosamente tu primer proyecto basado en Starling.

.Fantástico: un Starling rojo en una caja roja.
image::hello-world.png["Hello World", pdfwidth='7cm']

Hablando en serio: La parte más desalentadora ya pasó.
Finalmente, estamos listos para sambullirnos en un proyecto real!
