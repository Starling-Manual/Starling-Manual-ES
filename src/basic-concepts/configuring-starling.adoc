== Configurando Starling

El primer paso de cada aplicación hecha con Starling: crear una instancia de la clase _Starling_ (paquete `starling.core`).
Esta es la declaración completa del constructor de Starling:

[source, as3]
----
public function Starling(
    rootClass:Class,
    stage:Stage,
    viewPort:Rectangle = null,
    stage3D:Stage3D = null,
    renderMode:String = auto,
    profile:Object = auto);
----

rootClass:: La clase instanciada al momento que _Stage3D_ termina de inicializarse. Tiene que ser una subclase de `starling.display.DisplayObject`.

stage:: El tradicional escenario [stage] de Flash que albergará el contenido de Starling. Así es como el display list de Starling y Flash se conectan mútuamente.

viewPort:: El área en el stage [escenario] de Flash en la que Starling va a ser renderizado. Ya que esta es casi siempre el tamaño completo del stage, es un parámetro que podes omitir (por ej: pasando `null`) para usar toda el área disponible.

stage3D:: La instancia _Stage3D_ usada para renderizar. Cada stage de Flash puede contener varias instancias _Stage3D_, y podés elegir cualquiera. Pero el parámetro por defecto (`null`) es más que suficiente la mayoría de las veces: esto hará que Starling use el primer objeto _Stage3D_ disponible.

renderMode:: La idea detrás de _Stage3D_ es proveer de una renderización acelerada por hardware. Pero también dispone de un modo de "recuperación" por software; el que puede ser forzado pasando `Context3DRenderMode.SOFTWARE`. Aunque el valor por defecto (`auto`) es recomendado, -- esto significa que la renderización por software sólo es usada cuando no hay otra alternativa.

profile:: _Stage3D_ provee una serie de capacidades que están agrupadas en _perfiles_ [_profiles_] diferentes (definidos como constantes dentro de `Context3DProfile`). A mejor hardware usado para correr la aplicación, mejor es el perfil disponible. El valor por defecto (`auto`) simplemente elige el mejor perfil disponible.

La mayoría de estos argumentos tienen valores por defecto útiles, así que probablemente no necesites especificarlos todos.
El siguiente código muestra la forma más directa de arrancar Starling.
Estamos viendo la clase _Main_ de un proyecto _Flash Player_ o _AIR_.

[source, as3]
----
package
{
    import flash.display.Sprite;
    import starling.core.Starling;

    [SWF(width="640", height="480",
         backgroundColor="#808080",
         frameRate="60")]
    public class Main extends Sprite
    {
        private var _starling:Starling;

        public function Main()
        {
            _starling = new Starling(Game, stage);
            _starling.start();
        }
    }
}
----

Fijate que la clase extiende `flash.display.Sprite`, no la variante Starling.
Eso es simplemente un requerimiento de toda clase _Main_ en _AS3_.
Sin embargo, al momento que Starling termina de configurarse, toda la lógica es movida dentro de la clase _Game_, lo cual genera nuestro enlace al mundo `starling.display`.

[NOTE]
.Configurando los Cuadros por Segundo [Frame Rate]
====
Algunas configuraciones son asignadas directamente en la etiqueta _MetaData_ del "SWF" arriba de las clases.
Los cuadros por segundo es una de ellas.
En sí, Starling, no dispone de una configuración equivalente: siempre usa el _frameRate_ del stage nativo.
Para modificarlo en tiempo de ejecución, puedes acceder a la propiedad `nativeStage`:

[source, as3]
----
Starling.current.nativeStage.frameRate = 60;
----
====

El proceso que prepara a Starling is asincrónico.
Esto significa que no vas a poder acceder inmediatamente a la instancia `Game` al final del método en `Main`.
Pero, podes escuchar el evento `ROOT_CREATED` para ser notificado cuando la clase haya sido instanciada.

[source, as3]
----
public function Main()
{
    _starling = new Starling(Game, stage);
    _starling.addEventListener(Event.ROOT_CREATED, onRootCreated);
    _starling.start();
}

private function onRootCreated(event:Event, root:Game):void
{
    root.start(); // 'start' necesita ser definida en la clase 'Game'
}
----

=== La Ventana de Visualización [ViewPort]

_Stage3D_ provee a Starling de un área rectangular para que dibuje gráficos.
Esta área puede estar en cualquier lugar dentro del stage nativo, lo que significa: cualquiera lugar dentro del área del _Flash Player_ o de la ventana de la aplicación (en caso de un proyecto _AIR_).

En Starling, esta área es llamada el _viewPort_.
Mayormente, vas a querer usar toda el área disponible, pero a veces tiene sentido limitarse a renderizar una región en particular.

Imagináte un juego diseñado en una relación de aspecto 4:3, corriendo en una pantalla de 16:9.
Centrando el _viewPort_ 4:3 en pantalla, vas a ver un juego en modo "buzón" ["letterboxed"], con barras negras en los laterales.

// TODO: add image

No podemos hablar del _viewPort_ sin considerar también al _stage_ de Starling.
Por defecto, el stage va a tener exactamente el mismo tamaño que el _viewPort_.
Esto, por supuesto, tiene mucho sentido: un dispositivo con un tamaño de pantalla de 1024 x 768 píxeles debería tener un escenario del mismo tamaño.

Pero, sin embargo, podes personalizar el tamaño del stage.
Eso es posible gracias a las propiedades `stage.stageWidth` y `stage.stageHeight`:

[source, as3]
----
stage.stageWidth = 1024;
stage.stageHeight = 768;
----

Un momento, qué significa esto?
El tamaño del área de dibujo está definido por el tamaño del _viewPort_ o del _stage_?

No te preocupes, el área sólo sigue siendo definida por el _viewPort_, como explicamos arriba.
Modificar el `stageWidth` y `stageHeight` no cambia el tamaño del área de dibujo para nada;
el stage siempre es extendido a lo largo de todo el _viewPort_.
Lo que sí estas cambiando, es el tamaño del _sistema de coordenadas_ del stage.

Esto significa que con un ancho de stage de `1024`, un objeto con una coordenada x de `1000` va a estar más cerca al borde derecho del stage; no importa si el viewPort mide `512`, `1024`, o `2048` píxels de ancho.

Esto se vuelve especialmente útil cuando desarrollamos para pantallas _HiDPI_.
Por ejemplo, el _iPad_ de Apple existe en versión normal y "retina", la última duplica la cantidad de pixeles en filas y columnas (mostrando cuatro veces más cantidad de píxels).
En este tipo de pantallas, los elementos de la interfaz no deberían volverse más chicos; sino que deberían renderizarse con más nitidez.

Al diferenciar entre _viewPort_ y _tamaño del stage_, esto es muy fácil de reproducir en Starling.
En ambos tipos de dispositivo, el tamaño del stage va a ser 1024×768; pero el viewPort, en cambio, va a reflejar el tamaño de la pantalla en píxeles.
La ventaja: podés usar las mismas coordenadas para tus objetos visuales, independientemente del dispositivo que está corriendo tu aplicación.

[NOTE]
.Puntos vs. Píxeles
====
Si lo pensas detenidamente, vas a ver que en los dispositivos retina, un objeto con coordenada x=`1` va a estar efectivamente a 2 píxeles del orígen.
En otras palabras, la unidad de medida ha cambiado.
Ya no estamos hablando de píxels, sino _puntos_!
En una pantalla de baja resolución, un punto equivale a un píxel; en una pantalla HiDPI, son dos píxeles (o más, dependiendo del dispositivo).
====

Para descrifrar el ancho real (en píxeles) de un punto, simplemente dividí `viewPort.width` por `stage.stageWidth`.
O podés usar la propiedad `contentScaleFactor` de Starling, que hace justamente eso.

[source, as3]
----
starling.viewPort.width = 2048;
starling.stage.stageWidth = 1024;
trace(starling.contentScaleFactor); // -> 2.0
----

Ya te voy a mostrar como exprimir este concepto al máximo en el capítulo de <<Mobile Development>>.

=== Perfíles Context3D

Las plataformas en las que Starling está corriendo, presentan una gran variedad de procesadores gráficos.
Por supuesto, cada GPU tiene prestaciones distintas.
La pregunta es: como diferenciamos entre esas prestaciones en tiempo de ejecución?

Para eso están los _Perfíles Context3D_ (también llamados _perfiles de renderización_).

[NOTE]
.Qué es un Context3D?
====
Cuando usamos _Stage3D_, estás interactuando con un conducto de información de renderización [rendering pipeline] que presenta un número de propiedades y configuraciones.
El _contexto_ es el objeto que encapsula este conducto.
Crear una textura, subir un shader, renderizar triángulos -- todo es hecho através del contexto.
====

En realidad, Starling hace todo esfuerzo para ocultarte cualquier limitación del perfíl.
Para asegurarse el mayor alcance, fue diseñado para trabajar hasta con el perfíl más bajo disponible.
Al mismo tiempo, cuándo está corriendo con un perfíl superior, automáticamente, va a exprimirlo al máximo.

De cualquier forma, puede ser útil conocer las prestaciones básicas.
Acá tenemos una visión general de cada perfíl, empezando con el más bajo:

`BASELINE_CONSTRAINED`:: Si un dispositivo soporta Stage3D, tiene que poder usar este perfíl. Acarrea muchas limitaciones mezquinas, por ej: sólo soporta texturas con longitudes laterales forzadas a exponentes de dos, y la longitud de los shaders es muy limitada. Este perfíl se encuentra mayormente en antiguas computadoras de escritorio.

`BASELINE`:: El perfíl mínimo que podrás encontrar en dispositivos móviles. Starling corre bien con este perfíl; al eliminar la limitación del tamaño forzado a exponentes de dos le permite un manejo más eficiente del uso de la memoria, y la longitud de los programas shaders es más que suficiente.

`BASELINE_EXTENDED`:: Aumenta el tamaño máximo de texturas de `2048x2048` a `4096x4096` pixeles, lo cual es crucial para dispositivos con alta resolución.

`STANDARD_CONSTRAINED`, `STANDARD`, `STANDARD_EXTENDED`:: Starling actualmente no necesita de ninguna de las prestaciones que vienen con estos perfíles. Estos proveen de comandos shaders adicionales y otras mejoras a bajo nivel.

Mi recomendación: simplemente deja que Starling elija el mejor perfíl disponible (`auto`) y dejálo que maneje las consecuencias.

[NOTE]
.Tamaño de Texture Máximo
====
Sólo te vas a tener que hacer cargo de una cosa: asegurate que tus texturas no sean demasiado grandes.
El tamaño máximo de textura es accesible por la propiedad `Texture.maxSize`, pero sólo _después_ que Starling haya finalizado su inicialización.
====

=== Capa Nativa [Native Overlay]

La idea central detrás de Starling es acelerar la renderización con su API manejada por Stage3D.
Pero, no lo neguemos: el display list clásico tiene demasiads funcionalidades que Starling simplemente no puede ofrecer.
Entonces, tiene sentido ofrecer una forma sencilla de mezclar las prestaciones de Starling y el clásico Flash.
La propiedad `nativeOverlay` es la forma más fácil de hacerlo.
Es simplemente un `flash.display.Sprite` convencional, puesto encima de Starling, que contempla el _viewPort_ y _contentScaleFactor_.
Si necesitas usar objetos Flash convencionales, agregálos a esta capa.

Pero tené cuidado, contenido de Flash convencional por encima de _Stage3D_ puede llevar a una penalización de rendimiento en algunas plataformas (móviles). Por esta razón, siempre elimina los objetos de esta capa cuando no los necesites más.

[NOTE]
====
Antes que preguntes: no, no podés agregar objetos visuales convencionales de Flash _debajo_ de objetos visuales de Starling.
La superficie (o capa) de Stage3D esta siempre al fondo; y no hay forma de cambiarlo.
====

=== Salteando Fotogramas Sin Cambios

Pasa sorprendentemente seguido en una aplicación o juego que una escena se mantiene completamente estática por varios fotogramas.
La aplicación podría estar mostrando una pantalla estática o esperando por una entrada del usuario, por ejemplo.
Entonces para qué redibujar el stage en estás situaciones?

Ese es exáctamente el punto de la propiedad `skipUnchangedFrames`.
Si esta habilitada, escenas estáticas son reconocidas como tal y el back buffer queda como está.
En un dispositivo móvil, el impacto de esta prestación no se puede sobreestimar.
No existe mejor forma de mejorar la duración de la batería!

Ya estoy escuchando tu objeción: si esta prestación es tan útil, por qué no está activada por defecto?
Tiene que haber una trampa, no

Así es, la hay: no trabaja muy bien con _Render-_ y _VideoTextures_.
Los cambios en esas texturas simplemente no se verán.
Igual es fácil compensar la limitación: o deshabilitas `skipUnchangedFrames` temporalmente mientras usas estas texturas, o llamas a `stage.setRequiresRedraw()` cuando el contenido de las texturas cambie.

Ahora que ya sabés todo sobre esta característica, hacéte del hábito de activarla siempre!
Mientras tanto, espero poder resolver los mencionados problemas en una versión futura de Starling.

IMPORTANT: En plataformas móviles, hay otra limitación que deberías considerar: al momento que haya contenido en el stage (Flash) nativo (por ej: através del `nativeOverlay` de Starling), Starling no puede saltear ningún fotograma.
Esa es una consecuencia de una limitación en Stage3D.

=== El Monitor de Estadísticas

Cuando desarrollas una aplicación, quieres la mayor cantidad de información posible sobre lo que está ocurriendo.
De esta forma, vas a ser capaz de encontrar los problemas tempranamente y quizás evitar encontrarte en un callejón sin salida después.
El monitor de estadísticas te ayuda con eso.

[source, as3]
----
_starling.showStats = true;
----

.El monitor de estadísticas (por defecto arriba a la izquierda).
image::stats-display.png[El monitor de estadísticas, pdfwidth='4cm']

Cuál es el sentido de esos valores?

* El _framerate_ [fotogramas] debería ser bastante obvio: el número de fotogramas que Starling pudo renderizar durante el segundo previo.
* _Standard memory_ [memoria estándar] es, en pocas palabras, lo que tus objetos _AS3_ consumen. Ya sea un _String_, un _Sprite_, un _Bitmap_, o una _Function_: todos los objetos requiren algo de memoria.
 El valor es presentado en megabytes.
* _GPU memory_ está separado del anterior. Texturas almacenadas en la memoria gráfica, como así también los programas shader y vertex buffers.
  La mayoría del tiempo, las texturas opacarán todo lo demás.
* El número de _draw calls_ [llamadas de redibujado] indica cuántas llamadas a comandos individuales de "draw" [dibujado] son envíadas al GPU en cada fotograma.
  Tipicamente, una escena se renderiza más rápido cuándo hay menos draw calls.
  Ya veremos en detalle este valor cuando hablemos sobre <<Performance Optimization>>.

Quizás notes que el color de fondo del monitor de estadísticas alterna entre negro y verde oscuro.
Esa es una pista sutil que refiere a la propiedad `skipUnchangedFrames`:
siempre que la mayoría de los últimos pares de fotogramas puedan ser salteados; el fondo se vuelve verde.
Asegurate que se mantenga verde siempre que la escena es estática; si no lo hace, alguna lógica está previniendo la función del salto de fotogramas.

TIP: Podés ajustar la localización del monitor de estadísticas en pantalla con el método `showStatsAt`.

